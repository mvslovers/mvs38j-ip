         MACRO
&LABEL   WATGD &PFX=TGD,&PRINT=OFF,&GEN=,&DATA=,&SECT=DSECT,&TGDL=544
.*
.* Copyright 2002 James M. Morrison
.*
         GBLB  &G#TGD                  WATGD generated
         LCLC  &P                      TGD prefix selected;
.*
         AIF   (NOT &G#TGD).CONT
         MEXIT ,
.*
.CONT    ANOP
&G#TGD   SETB  1                       TGD generated
.*
&P       SETC  '&PFX'                  TGD prefix
         PUSH  PRINT                                              WATGD
         PRINT &PRINT,&GEN.GEN,&DATA.DATA                         WATGD
         DSAEX ,                       need L$SXPFX (generated once)
***********************************************************************
*  TGD: xxxx370 Task Global Data - based R11
*  Provides support for U* macros & U370xxx CSECTs
***********************************************************************
&LABEL   IM#SECT &SECT
&P.WA    DS    0D                      U#SET FUNC=UTIL support
UTILWA   DS    0D                      U#SET FUNC=UTIL support
&P.EYE   DC    CL8'xxxxDSA'            eyecatcher                 UPROC
&P.LDSA  DS    F                       bytes gotten for DSA pool  UPROC
&P.RC    DS    F                       xxxx370 return code
&P.@PARM DS    A                       address of OS entry parm   UPROC
&P.@VTAB DS    A                       VCON table              U370AP00
&P.@TCB  DS    A                       @ owning TCB            U370AP00
&P.LOGD  DS    CL8                     U370LOG ddname or 0
         DS    2F                      spare
*
         DS    0D
&P.UPFX  DS    XL(L$SXPFX)             xxxx370 SX                 UPROC
&P.SA1   DS    18F                     xxxx370 SA                 UPROC
*----------------------------------------------------------------------
*  Global status flags
*----------------------------------------------------------------------
&P.FLG1  DS    X                       notable events
&P.1ABND EQU   128                     UABEND requested
&P.1XNFR EQU   64                      U370AP99 - don't FREEMAIN pool
&P.1XABD EQU   32                      U370AP99 - ABEND, not return
*
&P.FLG2  DS    X                       U370QENV status flags
&P.2QENV EQU   128                     U370QENV called, TGD2V* set
&P.2VAPF EQU   32                      TGD2APF valid
&P.2APF  EQU   16                      APF authorized
&P.2VHEP EQU   8                       TGD2HEP valid (APF required)
&P.2HEP  EQU   4                       Hercules emulator present
*
&P.FLG3  DS    X                       U370STAE (only) status
&P.3RETY EQU   128                     U370STAE ESTAE active
&P.3SUPT EQU   64                      SUPPORT active
&P.3SCHD EQU   32                      ESTAE retry exit scheduled
&P.3EXIT EQU   16                      SUBAIL should exit appl
&P.3LOCL EQU   8                       RETRY2 active (local)
&P.3RMT  EQU   4                       remote retry active
&P.3DIE  EQU   2                       RE2DIE ABENDing
*
&P.FLG4  DS    X                       U370* behavior modification
&P.4NSTA EQU   128                     don't establish any ESTAEs
*
&P.FLG5  DS    X                       USTATE state recording
&P.5SUPR EQU   128                     supervisor state (U370AP00/99)
*
&P.FLG6  DS    X                       spare
&P.FLG7  DS    X                       spare
&P.FLG8  DS    X                       spare
*----------------------------------------------------------------------
*  Debug areas
*----------------------------------------------------------------------
         DS    0D
&P.SNAP  USNAP MF=L,PLIST=WKSNAP       common U370SNAP parmlist
*
         DS    0D
&P.REGS  DS    16F                     R0:R15 - UDIAG REGS=
*
         DS    0D
&P.ABEND DS    16F                     R0:R15 - pre-ABEND (UABEND)
*----------------------------------------------------------------------
*  U370STAE work areas
*----------------------------------------------------------------------
         DS    0D
&P.ESRGS DS    16F                     SUPPORT regs R0:R15
&P.ES@AX DS    A                       @ ABENDing SX
&P.ES@RX DS    A                       @ remote retry SX
&P.ES@SP DS    A                       zero or @ SNAP parmlist
&P.ES@SL SNAP  MF=L                    SNAP parmlist
&P.ES@SS DS    4D                      SNAP storage LIST=
*----------------------------------------------------------------------
*  Miscellaneous
*----------------------------------------------------------------------
* U370QENV:
&P.PGLV  DS    F                       subpool/length for FREEMAIN
&P.@PAGE DS    A                       0 or addr of page-aligned 4K
*                                      not currently freed by UPROC;
*                                      allocated by U370QENV
&P.PGPRM EQU   &P.PGLV,8               LM target for FREEMAIN
TGDSAVER DS    CL4                     U370AP00 SA verification field
*----------------------------------------------------------------------
*  Reserved for zap usage
*----------------------------------------------------------------------
         DS    0D
&P.ZAP   DS    XL32                    zap workarea
*----------------------------------------------------------------------
*  Debug bytes
*----------------------------------------------------------------------
.*
.*  Debug level bytes, set by U370DBUG, tested by UDIAG & UPSR
.*  One byte per CSECT that issues UDIAG or UPSR
.*  Values range from C'0' (no messages) or C'9' (most verbose)
.*  UDIAG & UPSR manufacture the debug byte name from CSECT name
.*
.*  ASMF seems to be too silly to let us generate an opcode containing
.*  '#' (American pound sign) ... hardcoding the macro name works fine
*
&P.DEBUG DS    0F
         U370#BUG ,
&P.APPLD DS    0C
         XMIT#BUG ,
         ORG   &P.APPLD
         RECV#BUG ,
         ORG   &P.APPLD
         DAST#BUG ,
         ORG   &P.APPLD
         STCP#BUG ,
         ORG   ,
&P.DEND  DS    0C                      end of debug bytes
&P.#VTE  EQU   *-&P.DEBUG              # CSECTs - for U370AP00
         DS    0D                      in case TGD larger than &TGDL
*----------------------------------------------------------------------
*  End of TGDWA
*----------------------------------------------------------------------
         ORG   &P.WA
         DS    XL&TGDL                 desired TGD size (unless larger)
         ORG   ,
&P.X     DS    0D                      end of TGDWA
L$&P     EQU   *-&P.WA
*
         POP   PRINT                                              WATGD
.DONE    MEXIT ,
.*---------------------------------------------------------------------
.*  WATGD - TGD Data Area - Task Global Data
.*
.*  The TGD is intended to provide common work areas for the U370*
.*  CSECTs, and other handy things.  It should remain relatively small
.*  so it doesn't impinge on the application specific TGD additions
.*  (WAXMIT, etc).  Since we can only cover 4KB with a single base
.*  register, the total size of the TGD and the application specific
.*  additions must remain under 4KB to be addressable.
.*
.*  Beginning in v0005, U370* CSECTs should place whatever data
.*  they can into their CSECT-specific persistent data area,
.*  rather than clutter up the TGD.
.*---------------------------------------------------------------------
         MEND  ,
