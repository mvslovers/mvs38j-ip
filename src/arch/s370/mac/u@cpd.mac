         MACRO
&LABEL   U@CPD &USER,&REG,&WORK=R0,&SAREG=SAREG,&ERR=
.*
.* Copyright 2002 James M. Morrison
.*
         AIF   ('&ERR' EQ '').ERR1
         AIF   ('&REG' EQ '').ERR2
&LABEL   U@SX  &REG,WORK=&WORK              @ SX                  U@CPD
         ICM   &REG,15,SX@CPD-SX(&REG)      @CPD                  U@CPD
         BZ    &ERR                         br, no CPD present    U@CPD
         USING CPD,&REG                                           U@CPD
         AIF   ('&USER' NE '').USER
         MEXIT
.*
.USER    LA    &REG,CPDUSER                 @ user portion of CPD U@CPD
         USING &USER,&REG                                         U@CPD
         MEXIT
.*
.ERR1    ANOP
         MNOTE 8,'U@CPD - ERR= required'
.*
.ERR2    ANOP
         MNOTE 8,'U@CPD - register required'
         MNOTE *,'U@CPD - syntax: U@CPD dsect,register,WORK=workreg'
.DOC     ANOP
.*---------------------------------------------------------------------
.*  U@CPD  - Return address of CPD or CPDUSER
.*           USING provided on either CPD or &USER
.*
.*  Note:    not every CSECT has a CPD
.*
.*  &USER  - optional, name of DSECT describing area
.*           if specified, USING will be &USER (@ CPD user area)
.*           if omitted,   USING will be CPD   (@ CPD base)
.*
.*  &REG   - register to contain address
.*
.*  &WORK  - work register
.*
.*  &SAREG - default=SAREG; register pointing at SA
.*           passed to U@SX
.*
.*  &ERR   - required; default=
.*           Specifies label to branch to if no CPD
.*---------------------------------------------------------------------
         MEND
