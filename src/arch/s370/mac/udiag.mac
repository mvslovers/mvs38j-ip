         MACRO
&LABEL   UDIAG &LEVEL,&TEXT,&ADDR,&LEN,&SKIP=,&REGS=NO,&PLIST=WKSNAP,  +
               &FIN=
.*
.* Copyright 2001, 2002 James M. Morrison
.*
         GBLB  &XMTDIAG
         GBLC  &XMTREGS
         GBLC  &GPFXTGD
         LCLA  &LDBUG,&LSTR
         LCLC  &NDX,&PFX
         LCLC  &DBUGBYT,&SKIPLAB
         LCLC  &NEWTEXT,&STRING
.*
&PFX     SETC  '&GPFXTGD'    TGD prefix
         AIF   (&XMTDIAG).GO
&LABEL   IM#SECT LABEL       generate label for suppressed UDIAG
         MEXIT
.*
.GO      ANOP
&NDX     SETC  '&SYSNDX'
&LDBUG   SETA  K'&SYSECT
&DBUGBYT SETC  '&SYSECT'(5,&LDBUG-4)     set TGDD* byte name from CSECT
.*
.*  Put label for BL instruction into common format
.*
&SKIPLAB SETC  '&SKIP'
         AIF   ('&SKIP' NE '').NOSTR           SKIP=label ignores &TEXT
&SKIPLAB SETC  'SKIP&NDX'
.*
.*  Insert <n> into front of quoted string
.*
&NEWTEXT SETC  '&TEXT'
         AIF   ('&TEXT'(1,1) NE '''').NOSTR      skip if not quoted str
&LSTR    SETA  K'&TEXT-2                        length minus two quotes
&STRING  SETC  '&TEXT'(2,&LSTR)                strip quotes from string
&NEWTEXT SETC  '''<&LEVEL> &STRING'''              new text with quotes
.NOSTR   ANOP
.*
.*  Generate code
.*
&LABEL   IM#SECT LABEL
         AIF   ('&LEVEL' EQ '0' AND '&SKIP' NE '').MEXIT
         AIF   ('&LEVEL' EQ '0').STM        level=0 always runs
         CLI   &PFX.D&DBUGBYT,C'&LEVEL'     generate diag msg?
         BL    &SKIPLAB                     no, br
         AIF   ('&SKIP' EQ '').STM
.MEXIT   MEXIT
.*
.STM     AIF   ('&REGS' EQ 'NO').CKSNAP
         STM   R0,R15,&XMTREGS              <debug>
.*
.CKSNAP  AIF   ('&LEN' NE '').SNAP
         AIF   ('&TEXT'(1,1) EQ '''').STRLOG
         ULOG  &TEXT,&ADDR                  ULOG RX/(R),length
         AGO   .FIN
.STRLOG  ULOG  &NEWTEXT                     ULOG text string
         AGO   .FIN
.*
.SNAP    USNAP &NEWTEXT,&ADDR,&LEN,PLIST=&PLIST
.FIN     AIF      ('&FIN' EQ '').DONE
         B     &FIN                         output produced, br
.DONE    ANOP
SKIP&NDX DS    0H
         MEXIT
.*---------------------------------------------------------------------
.* &LEVEL: 0   normal non-diagnostic messages (always shown)
.*         1   STATUS messages, entry & exit
.*         2   CSECT-SPECIFIC trace messages
.*     above   varies by CSECT
.*
.* During execution, if the proper debug level is not set in TGDWA,
.* the U370LOG or U370SNAP calls will not be executed
.*
.* &TEXT - RX or quoted string message to be issued, using either
.* calls to U370LOG or U370SNAP.  For RX specifications, see U370LOG
.* or U370SNAP for the layout of the message (they're different).
.* &TEXT will be displayed by U370LOG, unless &LEN is coded in which
.* case U370SNAP will display the text.
.* Briefly, U370LOG - RX field consists of text only.
.* U370SNAP - RX field has one byte length prefix, or may be delimited
.* by X'00' at the end (then no length prefix); 60 byte max text.
.*
.* &ADDR - U370SNAP start address
.*         An exception - if &LEN isn't coded, assumed to be length
.*         of non-quoted string &TEXT (ULOG issued, not USNAP)
.*
.* &LEN  - U370SNAP length (or stop address)
.*
.* REGS=NO suppresses STM of R0:R15 into &XMTREGS, code some other
.* value to generate STM (REGS=YES, REGS=SAVE, etc.).
.*
.* SKIP=label causes branch to label if diagnostic level msg would
.* not be issued; &TEXT, &ADDR, &LEN &REGS ignored when used.
.* SKIP=label would be used to branch around code only needed for
.* certain diag levels.
.*
.* FIN=label  causes branch to label after completion of UDIAG.
.*            Handy for branching around lower-level diagnostics
.*            immediately following higher-level diagnostics.
.*            Helpful in filtering out lower-level diags (ex: 4)
.*            that produce (redundant) reduced output after
.*            higher-level diags (ex: 9).
.*---------------------------------------------------------------------
         MEND
