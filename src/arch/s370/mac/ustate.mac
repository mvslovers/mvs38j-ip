         MACRO
&LABEL   USTATE &MF=,&SA=SAUSTATE,&STPROB=,&STSUPR=,&KEY=,&SAVE=Y
.*
.* Copyright 2002 James M. Morrison
.*
.*---------------------------------------------------------------------
.* &MF     - GEN generates state change routines
.*         - L   generates state change routines' savearea
.* &SA     - label of savearea for generated code use
.* &STPROB - Y generates STPROB routine
.* &STSUPR - Y generates STSUPR routine
.* &KEY    - 0-15 - issues SPKA (requires supervisor state)
.*           &MF will not be examined when KEY= specified
.* &SAVE   - Y - default; saves work regs in workarea
.*           N - doesn't
.* Issuer is assumed to be APF authorized (TGD2APF)
.*---------------------------------------------------------------------
         AIF   ('&KEY' EQ '').CKGEN
&LABEL   SPKA  &KEY*16                 set PSW protect key (priv)
         MEXIT
.CKGEN   AIF   ('&MF' EQ 'L').MFL
         AIF   ('&MF' NE 'GEN').FIN
         AIF   ('&STPROB' NE 'Y').CKSUPR
*----------------------------------------------------------------------
*  Assure problem state
*----------------------------------------------------------------------
STPROB   DS    0H
         TM    TGDFLG5,TGD5SUPR        SUPERVISOR state?
         BNOR  R14
         AIF   ('&SAVE' EQ 'N').MODESET
         STM   R14,R1,&SA
.MODESET MODESET MODE=PROB
         AIF   ('&SAVE' EQ 'N').NI
         LM    R14,R1,&SA
.NI      NI    TGDFLG5,255-TGD5SUPR    PROBLEM state
         BR    R14
.CKSUPR  AIF   ('&STSUPR' NE 'Y').FIN
*----------------------------------------------------------------------
*  Assure supervisor state
*----------------------------------------------------------------------
STSUPR   DS    0H
         TM    TGDFLG5,TGD5SUPR        SUPERVISOR state?
         BOR   R14
         STM   R14,R1,&SA
         MODESET MODE=SUP
         LM    R14,R1,&SA
         OI    TGDFLG5,TGD5SUPR        supervisor state
         BR    R14
.FIN     MEXIT
*
.MFL     ANOP
&SA      DS    2D                      USTATE - workarea
*
         MEND
