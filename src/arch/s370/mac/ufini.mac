         MACRO
&CSECT   UFINI &OBS,&LSA,&ALIGN=32,&ID=,&SECT=,&OPT=0
.*
.* Copyright 2001, 2002 James M. Morrison
.*
         GBLC  &GUPROCP(8)                  UPROC CPD=
         GBLC  &GUPROCI                     UPROC ID=
         LCLC  &TSA,&TSX,&TCPD
         LCLC  &LCPD
.*
         AIF   ('&SECT' EQ 'DSECT').DSECT
         PUSH  PRINT
         PRINT ON,GEN
&CSECT   CSECT ,                            resume CSECT after DSECTs
         AIF   ('&ID' EQ 'TGD').TGD
.*
.*  Normal CSECT's CPD & DSA
.*  It is important that we define the SX PR immediately before
.*  the SA PR; the SX is located by: A(SA) - L$SXPFX
.*
*----------------------------------------------------------------------
*  FINIBLOK - CSECT attributes
*----------------------------------------------------------------------
FINIBLOK DS    0F
&LCPD    SETC  '0'                          assume no CPD
         AIF   ('&GUPROCP(1)' EQ 'N').FINCPD
&LCPD    SETC  'L$CPD'                      CPD exists, has length
.FINCPD  ANOP
&TCPD    SETC  'XCP'.'&CSECT'(1,1).'&CSECT'(5,K'&CSECT-4)
&TCPD    U#DPR QCPD,&LCPD                   define CPD (UPROC)
.*
&TSX     SETC  'XSX'.'&CSECT'(1,1).'&CSECT'(5,K'&CSECT-4)
QDSA     DS    0F                           SX at top of DSA
&TSX     U#DPR QSX,L$SXPFX                  define SX
.*
&TSA     SETC  'XSA'.'&CSECT'(1,1).'&CSECT'(5,K'&CSECT-4)
&TSA     U#DPR QSA,&LSA                     define SA
.*
         DC    AL2(10+*-&CSECT)         +0C CSECT length (approximate)
         DC    AL2(&LCPD)               +0E CPD   length
         DC    AL2(&LSA)                +10 SA    length
FBLSACL  DC    AL2(&LSA-(SXR0-SXBASE))  +12 SA    length to clear
         DC    AL1(&OPT,0)              +14 CSECT options, spare
         POP   PRINT
         MEXIT
.*
.DSECT   ANOP
FINBLOK  DSECT ,             UFINI FINIBLOK
FINQCPD  DS    F       +00   CSECT's CPD offset (0=no CPD)
FINQSX   DS    F       +04   CSECT's SX  offset
FINQSA   DS    F       +08   CSECT's SA  offset
FINLCS   DS    H       +0C   CSECT's length (approximate)
FINLCPD  DS    H       +0E   CSECT's CPD length
FINLSA   DS    H       +10   CSECT's SA  length
FINLSACL DS    H       +12   CSECT's SA  length to clear
FINOPT1  DS    X       +14   CSECT's options
         DS    X       +15   spare
         DS    0D
L$FIN    EQU   *-FINBLOK     FINBLOK length
         MEXIT ,
.*
.*  TGD - Task Global Data - Must be first PR defined in ID=MAIN
.*        ID=MAIN must be first object deck LKED encounters
.*
.TGD     ANOP
*
*  Note: it is important that the first DXD which LKED encounters
*  be the TGD, so that it assigns offset zero to the TGD.
*  UPROC ID=MAIN verifies the TGD's offset = 0, ABENDs if not
*
XTGD     U#DPR QTGD,&LSA
LDSAPOOL CXD   ,    Length of DSA pool calculated by LKED         UFINI
*                   (sum of Q-con lengths)                        UFINI
         POP   PRINT
         MEXIT
.DOC     ANOP
.*---------------------------------------------------------------------
.* Define pseudo-register & Q-con(s) for our DSA
.* If the CSECT's UPROC specified non-zero TGD length, define a
.* QCON which references the TGD
.*
.* &CSECT - CSECT name to which this pseudo-register applies
.*
.* &OBS   - obsolete, ignored (DXD name manufactured)
.*          used to be the DXD label
.*
.* &ID    - TGD - defines TGD PR (only UPROC ID=MAIN should use)
.*              - defines normal PR
.*
.* &LSA   - length of the SA pseudo-register (excludes SX prefix)
.*
.*          - For a DSA, excludes SX prefix length; the actual
.*            length will contain room for SX prefix
.*
.*          - UFINI internally generates a CPD for each CSECT;
.*            the CPD length (L$CPD) is calculated in DSATGD
.*            (currently based on UPROC CPD specification)
.*
.* &SECT  - null  - generate code
.*          DSECT - generate DSECT
.*
.*---------------------------------------------------------------------
         MEND
