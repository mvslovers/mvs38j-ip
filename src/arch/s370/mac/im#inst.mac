         MACRO
&LABEL   IM#INST &REG,&SPEC
.*
.* Copyright 2001, 2002 James M. Morrison
.*
.*---------------------------------------------------------------------
.* See notes below for details and syntax examples - ASMH+ version
.*---------------------------------------------------------------------
         LCLA  &N,&NN
         LCLA  &I
&LABEL   IM#SECT LABEL
&N       SETA  N'&SPEC
&NN      SETA  N'&SPEC(1)
         AIF   ((&N EQ 1) AND (&NN EQ 1)).LALR
         AIF   (&N GT 0).LOOP
         MNOTE 8,'IM#INSTR missing SPEC'
         MEXIT
.*
.LOOP    ANOP
&I       SETA  &I+1
         IM#IGEN &SPEC(&I,1),&SPEC(&I,2),&SPEC(&I,3),REG=&REG
         AIF   (&I LT &N).LOOP
         MEXIT
.*
.LALR    IM#LALR &REG,&SPEC
         MEXIT
.*---------------------------------------------------------------------
.* Function: Generate one or more instructions from specified (SPEC)
.* operands.
.*
.* &REG  - work register (may not always be used); generally the
.*         register into which the calling macro wishes to place
.*         a value.
.* &SPEC - instruction specification
.*
.* SPEC Syntax samples (REG=Rx):
.*
.* SPEC                      calls     generates
.* ------------------------- -------   --------------------------------
.* (R3)                      IM#LALR   LR    Rx,R3
.* FIELD                     IM#LALR   LA    Rx,FIELD
.* *FIELD                    IM#LALR   L     Rx,FIELD
.* .FIELD                    IM#LALR   LH    Rx,FIELD
.*
.* -CLEAR-                   IM#LALR   SLR   Rx,Rx
.* -ZERO-                    IM#LALR   SR    Rx,Rx
.* -0-                       IM#LALR   SR    Rx,Rx
.*
.* ((LH,FIELD))              IM#IGEN   LH    Rx,FIELD
.* ((ICM,8,FIELD))           IM#IGEN   ICM   Rx,8,FIELD
.* ((-ZERO-),(IC,F1))        IM#IGEN   SR    Rx,Rx
.*                                     IC    Rx,F1
.* ((MVI,F,8))               IM#IGEN   MVI   F,8
.* ((MVC,F1,F2))             IM#IGEN   MVC   F1,F2
.*
.* ((-0-),(IC,F),(OI,X,32))  IM#IGEN   SR    Rx,Rx
.*                           IM#IGEN   IC    Rx,F
.*                           IM#IGEN   OI    X,32
.*
.* The basic function of this macro is to allow our callers to offer
.* the coder the opportunity to "reach into" the macro expansion
.* to add a few instructions.  Some of the syntax is as old as the
.* hills: (Rx) and *FIELD have been in common usage since I started
.* coding, and I have carried those syntax forms forward.
.*
.* If you wish to generate more than one instruction, the syntax must
.* conform to the IM#IGEN format; the IM#LALR format is mostly for
.* backwards compatability which only requires one instruction.
.* For IM#IGEN, each sublist specified will generate one instruction.
.*
.* Not all instructions are capable of being generated by IM#INSTR.
.* IM#LALR only supports the LR, LA, L, and LH instructions.
.* IM#IGEN should support all of the RR, RX, and RS instruction types,
.* and a handful of others; see IM#IGEN for these details.
.* Both IM#IGEN and IM#LALR support the -x- forms.
.*
.* The coder is strongly encouraged to read the macro expansion to
.* assure that what gets generated is what was intended, at least
.* until some general familiarity with IM#INSTR is obtained.
.*---------------------------------------------------------------------
         MEND
