         MACRO
&LABEL   UWTO &LEVEL,&TEXT,&FIN=,&OPT=0
.*
.* Copyright 2002 James M. Morrison
.*
.*---------------------------------------------------------------------
.* &LEVEL - see UDIAG macro
.* &TEXT  - message to be written to MVS operator's console
.*          Quoted text string, or any operand supported by IM#LALR
.*          U370WTO defines the maximum text length, extra truncated
.* &FIN   - see UDIAG, same feature
.* &OPT   - U370WTO options, UWTO$* bits defined in WATGD
.*---------------------------------------------------------------------
         LCLC  &NDX
&NDX     SETC  '&SYSNDX'
&LABEL   UDIAG &LEVEL,SKIP=SKIP&NDX    gen CLI, BL or suppress
         IM#LALR R0,&OPT               set U370WTO options
         AIF   ('&TEXT'(1,1) EQ '''').STR
.*
         IM#LALR R1,&TEXT              R1 -> AL1 length, text
         UCALL U370WTO                 issue WTO, echo to xxxxLOG
         AIF   ('&FIN' EQ '').SKIP1
         B     &FIN                    output produced, br
.SKIP1   ANOP
SKIP&NDX DS    0H
         MEXIT ,
.*
.STR     AIF   ('&OPT' EQ '0').OPTOK
         MNOTE 8,'UWTO - options not supported with string'
         MNOTE *,'UWTO - if options desired, do not use string'
         SLR   R0,R0                   IGNORE OPTIONS
.OPTOK   IM#LALR R1,STRL&NDX           R1 -> AL1 length, text
         UCALL U370WTO                 issue WTO, echo to xxxxLOG
         AIF   ('&FIN' EQ '').SKIP2
         B     &FIN                    output produced, br
         AGO   .GENSTR
.SKIP2   B     SKIP&NDX                skip string
.GENSTR  ANOP
STRL&NDX DC    AL1(L'STR&NDX)          length of text
STR&NDX  DC    C&TEXT
.FIN     AIF   ('&FIN' EQ '').SKIP
         B     &FIN                    output produced, br
.SKIP    ANOP
SKIP&NDX DS    0H
         MEND
