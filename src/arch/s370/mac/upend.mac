         MACRO
&LABEL   UPEND &R0=RESTORE,&R1=RESTORE,&RC=,&DIE=,&FREE=,              +
               &ABEND=(370,DUMP),&PATCH=8
.*
.* Copyright 2001, 2002 James M. Morrison
.*
         GBLC  &GUPROCI           ID= (UPROC)
.*
&LABEL   IM#SECT LABEL            conditionally generate label
         AIF   ('&GUPROCI' NE 'MAIN').NORMAL
*----------------------------------------------------------------------
*  Terminate xxxx370 application (ID=MAIN)
*----------------------------------------------------------------------
         UCALL U370AP99                application termination
*                                      U370AP99 doesn't return to us
         MEXIT ,
.*
.NORMAL  AIF   ('&RC' EQ '').HVRC
         AIF   ('&RC' EQ '0').RC0
         IM#LALR R15,&RC               set return code
         AGO   .HVRC
.*
.RC0     SLR   R15,R15                 return code zero
.HVRC    L     R13,4(,R13)             R13 @ caller's SA
.NOR13   L     R14,12(,R13)            restore return addr
         AIF   ('&R1' NE 'RESTORE').NOR1
         AIF   ('&R0' NE 'RESTORE').NOR0
         LM    R0,R12,20(R13)          restore caller's regs (NOT R15)
         AGO   .XC
.*
.NOR0    LM    R1,R12,24(R13)        restore caller's regs (NOT R0,R15)
         AGO   .XC
.*
.*
.NOR1    AIF   ('&R0' NE 'RESTORE').R2
         L     R0,20(R13)              restore caller's regs
         LM    R2,R12,28(R13)          EXCEPT R1, R15
         AGO   .XC
.*
.R2      LM    R2,R12,28(R13)     restore caller's regs (NOT R15,R0,R1)
.*
.XC      XC    8(4,R13),8(R13)      erase caller's forward SA ptr to us
         AIF   ('&DIE' EQ '').BR14
*
         ABEND &ABEND(1),&ABEND(2),&ABEND(3)        <debug> DIE
         AGO   .SKIPRET
.BR14    BR    R14                     return to caller
.SKIPRET ANOP
*
         PUSH  PRINT
         PRINT DATA
PATCH    DC    &PATCH.S(*)             PATCH AREA <zap>
         POP   PRINT
.*---------------------------------------------------------------------
.*  xxxx370 exit linkage
.*  R15 is never restored
.*
.*  Special support for UPROC ID=MAIN
.*  See UPROC for further details
.*---------------------------------------------------------------------
         MEND
