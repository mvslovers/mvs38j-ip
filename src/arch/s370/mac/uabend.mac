         MACRO
&LABEL   UABEND &REASON,&DUMP=N,&STEP=N,&CODE=,&SVC=13
.*
.* Copyright 2001, 2002 James M. Morrison
.*
.*---------------------------------------------------------------------
.* &CODE   - ABEND code
.* &REASON - ABEND reason code, RX address only
.*
.* Normal OS DUMP, STEP options are not provided.  U370ABND makes
.* a decision on it's own, for ease in consolidating ABEND behavior.
.*---------------------------------------------------------------------
         GBLC  &XMTABND
         LCLC  &NDX
         LCLB  &A1,&A2,&B1,&B2
         LCLA  &F1,&F2
.*
&NDX     SETC  '&SYSNDX'
&LABEL   STM   R0,R15,&XMTABND         save regs before ABEND
         AIF   ('&CODE' EQ '').CKRSN
         IM#LALR  R1,&CODE             load abend code
.CKRSN   AIF   ('&REASON' EQ '').NOCOD
         ICM   R1,8,&REASON            load reason code
.NOCOD   UCALL U370ABND                call ABEND support
         SVC   &SVC                    U370ABND returns to ABEND here
.*
&A1      SETB  ('&DUMP' NE 'N')
&A2      SETB  ('&STEP' NE 'N')
.*
&B1      SETB  ('&REASON' NE '')
&B2      SETB  ('&CODE' NE '')
.*
&F1      SETA  (&A1*128)+(&A2*64)
&F2      SETA  (&B1*128)+(&B2*64)
.*
OPT&NDX  DC    AL1(&F1,&F2)            option flags
         MEXIT
.DOC     ANOP
.*---------------------------------------------------------------------
.*  UABEND - blah, blah, blah
.*  &SVC   - U370STAE codes SVC=3
.*---------------------------------------------------------------------
         MEND
