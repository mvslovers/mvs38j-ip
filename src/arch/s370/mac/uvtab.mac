         MACRO
&LABEL   UVTAB &CSECT,&OPT,&MF=
.*
.* Copyright 2002 James M. Morrison
.*
.*---------------------------------------------------------------------
.* UVTAB  - Define U370VTAB table entry
.*
.* &CSECT - CSECT
.* &OPT   - options
.* &MF    - DSECT - generate DSECT
.*        - HDR   - generate U370VTAB header
.*        - TLR   - generate U370VTAB trailer
.*        -       - generate U370VTAB VCON table element
.*---------------------------------------------------------------------
         LCLC  &BYT,&OPTVAL
.*
.*  Define U370VTAB table entry
.*
         AIF   ('&MF' NE '').NEL
&BYT     SETC  '&CSECT'(5,K'&CSECT-4)
&OPTVAL  SETC  '0'
         DC    CL8'&CSECT'
         DC    V(&CSECT)
         DC    AL2(TGDD&BYT-TGDDLEV)
         DC    AL2(&OPTVAL)
         DC    A(0,0,0,0)
*
         MEXIT
.*
.NEL     AIF   ('&MF' NE 'HDR').TLR
*
*  U370VTAB header
*
U370VTAB CSECT ,
         USING *,0
         USING TGDWA,11
HEADER   DC    A(DBUGTAB)
         DC    A(L$VTABE)
         DC    A(DBUGEND-1)
         DC    A(0)                    spare
*
DBUGTAB  DS    0D                      beginning of VCON table
         MEXIT
.*
.TLR     AIF   ('&MF' EQ 'DSECT').DSECT
         COPY  U370#VTB                bring in U370* VCONs
*
*  U370VTAB trailer
*
DBUGEND  DS    0C                      end of U370VTAB table
*
.DSECT   ANOP
VTB$XXXX EQU   0                       options
VTABLE   DSECT ,
*----------------------------------------------------------------------
*  CSECT table header - BXLE index, increment, limit
*----------------------------------------------------------------------
VTHDR    DS    0F            VCON table BXLE: index, increment, limit
VTINDEX  DS    A             beginning of table entries
VTINCR   DS    F             entry length
VTLIMIT  DS    A             last byte in table
         DS    A             spare
L$VTHDR  EQU   *-VTHDR       header length
VTENTRY1 EQU   *             beginning of 1st entry
         ORG   VTABLE
*----------------------------------------------------------------------
*  CSECT table
*----------------------------------------------------------------------
VTABNAME DS    CL8           CSECT name
VTABVCON DS    A             address of CSECT
VTABOFF  DS    H             offset to debug byte from XMTDLEV
VTABOPT1 DS    X             options
VTABOPT2 DS    X             options
VT@CPD   DS    A             @ CSECT's CPD                     U370AP00
VT@SX    DS    A             @ CSECT's SX                      U370AP00
VTCPDL   DS    H             CPD length           for U370VADR U370AP00
VTDSAL   DS    H             SX length            for U370VADR U370AP00
         DS    H             spare
VTCSL    DS    H             approx CSECT length  for U370VADR U370AP00
         DS    0D
L$VTABE  EQU   *-VTABLE      length of table entry
         ORG   ,
*
         AIF   ('&MF' EQ 'DSECT').DONE
         PRINT OFF
*        DSAEX ,
         DSAEX ,
*        WATGD ,
         WATGD ,
         PRINT ON
*
.DONE    MEND
