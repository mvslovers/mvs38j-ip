         MACRO
&LABEL   UESTAE &RRR,&SDWA=NO,&SNAP=NO,&TERM=NO,&MF=CODE,&PFX=CPD
.*
.* Copyright 2002 James M. Morrison
.*
         GBLC  &GUPROCP(8)             UPROC
         LCLA  &A
         LCLC  &P
.*
&LABEL   IM#SECT LABEL                 conditionally generate label
         AIF   ('&MF' EQ 'CPD').CPD    generate CPD fields
         AIF   ('&TERM' EQ 'NO').INIT
.*
.*  UESTAE TERM=YES
.*
         LA    R0,CPDETERM             U370STAE termination call
         AGO   .CALL
.*
.*  Setup CPDEFLG1 value R0(24:31)
.*
.INIT    AIF   ('&SDWA' EQ 'NO').CKSNAP
&A       SETA  128                     CPDEFLG1.CPDE1SDW
.CKSNAP  AIF   ('&SNAP' EQ 'NO').OPTFIN
&A       SETA  &A+64                   CPDEFLG1.CPDE1SNP
.OPTFIN  IM#LALR R0,&A                 ESTAE options
         IM#LALR R1,&RRR               get RRR addr
.CALL    UCALL U370STAE                call ESTAE support
         AIF   ('&GUPROCP(3)' NE 'ESTAE').EXPLAIN
         MEXIT ,
.EXPLAIN PUSH  PRINT
         PRINT ON
*                                                              (UESTAE)
*  CSECTs which either specify or allow UPROC CPD=N to default
*  must not request UESTAE services; doing so will overlay
*  either their own CPD data following here, or some poor
*  unsuspecting CSECT's SX (almost certainly their own)
*
*  If you wish no additional CPD space and no ESTAE support:
*  UPROC CPD=N
*  That's the default, so you may omit CPD=N
*
*  If you wish ESTAE support but no additional CPD space:
*  UPROC CPD=(Y,0,ESTAE)
*
*  If you wish additional CPD space but no ESTAE support:
*   UPROC CPD=(Y,xxx)
*
*  If you wish additional CPD space and ESTAE support:
*  UPROC CPD=(Y,xxx,ESTAE)
*
*  xxx is your desired CPD addition length
*
         POP   PRINT
         MNOTE 8,'UESTAE - UPROC CPD ESTAE not specified, ESTAE used'
         MNOTE 8,'UESTAE - see above explanation'
         MEXIT
.*
.CPD     ANOP
&P       SETC  '&PFX'
*
&P.ETERM EQU   256                UESTAE TERM=YES (R0:23)
*
&P.EFLG1 DS    X                  UESTAE options
&P.E1SDW EQU   128                RRR R1 @ SDWA/0; RRR must free
&P.E1SNP EQU   64                 U370STAE.SUPPORT: SNAP CPDE@SNP
*
&P.EFLG2 DS    X                  U370STAE status flags
&P.E2RRV EQU   128                RRR present + rest of ESTAE fields
&P.E2RRR EQU   64                 RRR called
*
         AIF   ('&GUPROCP(3)' EQ 'ESTAE').CPDE001
         MEXIT
.*
.*  These fields are only present when ESTAE support requested
.*
.CPDE001 ANOP
&P.E@RRR DS    A                  0 or @ CSECT's RRR         (U370STAE)
&P.E#RUN DS    F                  # times RRR run            (U370STAE)
&P.E@SNP DS    A                  CSECT's SNAP parmlist or 0 (U370STAE)
&P.EREGS DS    13F                RRR regs - R0:R12          (U370STAE)
*                                 R0=0 (reserved for future) (U370STAE)
*                                 R1=0 or addr of SDWA       (U370STAE)
         MEXIT ,
.DOC     ANOP
.*---------------------------------------------------------------------
.* UESTAE is the user interface to U370STAE ESTAE support
.* The user must request that the CPD be generated with ESTAE
.* support (see .EXPLAIN above)
.*
.* &RRR    - label  - Remote Retry Routine addr
.*
.* &SDWA   - NO     - default; don't give RRR SDWA in R1
.*           else   - RRR R1 @ SDWA (RRR must free)
.*
.* &SNAP   - NO     - default; U370STAE.SUPPORT won't SNAP CPDE@SNP
.*         - else   - CPDE@SNP @ SNAP MF=L parmlist (user must set)
.*
.* &MF     - CODE   - default; generate code
.*           CPD    - define CPD fields required for U370STAE support
.*
.* &TERM   - NO     - default; create/overlay ESTAE environment
.*                    as needed, sets up RRR pointer & ESTAE options
.*         - YES    - terminates TCB's ESTAE environment
.*                    usually only done in ID=MAIN code
.*
.* &PFX    - xxx    - default=CPD; 3 char label prefix for UESTAE
.*                    fields embedded in DSACPD
.*---------------------------------------------------------------------
         MEND
