         MACRO
&LABEL   ULOG &TEXT,&LENGTH,&TERM=NO
.*
.* Copyright 2001, 2002 James M. Morrison
.*
.*---------------------------------------------------------------------
.* &TEXT   - RX or quoted string.  RX fields consists of text only.
.* &LENGTH - optional, only needed to specify length if it can't
.*           be determined by the L' length attribute
.* &TERM   - YES closes xxxxLOG ddname
.*---------------------------------------------------------------------
         LCLC  &NDX
&NDX     SETC  '&SYSNDX'
.*
         AIF   ('&TERM' NE 'YES').CKSTR
         SLR   R1,R1                   TERM=YES, close LOG ddname
         UCALL U370LOG                 call U370LOG
         MEXIT ,
.*
.CKSTR   AIF   ('&TEXT'(1,1) EQ '''').STRING
.*
&LABEL   IM#LALR R1,&TEXT                        message text
         AIF   ('&LENGTH' EQ '').LENATTR
         IM#LALR R0,&LENGTH                      length of text
         AGO   .CALL
.*
.LENATTR LA    R0,L'&TEXT                        length of text
         AGO   .CALL
.*
.STRING  ANOP
&LABEL   BAL   R1,CODE&NDX                       skip msg, r1 @ msg txt
MSG&NDX  DC    C&TEXT
CODE&NDX LA    R0,L'MSG&NDX                              message length
.*
.CALL    UCALL U370LOG                                      log message
         MEND
