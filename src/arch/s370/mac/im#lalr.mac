         MACRO
&LABEL   IM#LALR &REG,&WHAT
.*
.* Copyright 2001, 2002 James M. Morrison
.*
.*---------------------------------------------------------------------
.* Function: Generate one instruction
.* &REG  - work register
.* &WHAT - (register), RX, or self-defining term to put in &REG
.* See IM#INSTR for further details
.*---------------------------------------------------------------------
         LCLA  &LWHAT
         LCLC  &TREG
         LCLC  &TWHAT
         AIF   ('&WHAT' NE '0').MORE
&LABEL   SLR   &REG,&REG                     zero register
         MEXIT
.MORE    ANOP
&TREG    SETC  '(&REG)'                      (LOWER '(&reg)')
&TWHAT   SETC  '&WHAT'                       (LOWER '&what')
.*       MNOTE 0,'IM#LALR REG &TREG'
.*       MNOTE 0,'IM#LALR WHAT &TWHAT'
         AIF   ('&TWHAT' EQ '&TREG').SKIP    already have value in reg
         AIF   ('&WHAT'      EQ '-CLEAR-').CLEAR
         AIF   ('&WHAT'      EQ '(-CLEAR-)').CLEAR
         AIF   ('&WHAT'      EQ '((-CLEAR-))').CLEAR
         AIF   ('&WHAT'      EQ '-ZERO-').ZERO
         AIF   ('&WHAT'      EQ '(-ZERO-)').ZERO
         AIF   ('&WHAT'      EQ '((-ZERO-))').ZERO
         AIF   ('&WHAT'      EQ '-0-').ZERO
         AIF   ('&WHAT'      EQ '(-0-)').ZERO
         AIF   ('&WHAT'      EQ '((-0-))').ZERO
         AIF   ('&WHAT'(1,1) EQ '(').LR
         AIF   ('&WHAT'(1,1) EQ '*').LOAD    *field means load field
         AIF   ('&WHAT'(1,1) EQ '.').LH      .field means load halfword
&LABEL   LA    &REG,&WHAT
         MEXIT
.*
.LR      ANOP
&LABEL   LR    &REG,&WHAT(1)
         MEXIT
.*
.LH      ANOP
&LWHAT   SETA  K'&WHAT-1
&TWHAT   SETC  '&WHAT'(2,&LWHAT)
&LABEL   LH    &REG,&TWHAT
         MEXIT
.*
.LOAD    ANOP
&LWHAT   SETA  K'&WHAT-1
&TWHAT   SETC  '&WHAT'(2,&LWHAT)
&LABEL   L     &REG,&TWHAT
         MEXIT
.*
.CLEAR   ANOP
&LABEL   SLR   &REG,&REG
         MEXIT
.*
.ZERO    ANOP
&LABEL   SR    &REG,&REG
         MEXIT
.*
.SKIP    ANOP
*              instruction suppressed; register already contains value
         MEND
