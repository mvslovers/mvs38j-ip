         MACRO
&LABEL   DSACPD &MF=DSECT,&SECT=DSECT,&PFX=CPD
.*
.* Copyright 2002 James M. Morrison
.*
         GBLC  &GUPROCP(8)         UPROC CPD=
         LCLC  &P
.*
         AIF   ('&GUPROCP(1)' EQ 'Y').GEN
         MEXIT ,                   CPD not requested
.GEN     ANOP
&P       SETC  '&PFX'
         AIF   ('&MF' EQ 'L').MFL
         PUSH  PRINT                                             DSACPD
         PRINT ON,GEN                                            DSACPD
*----------------------------------------------------------------------
*  CPD - Task Persistent Data header - based SX@CPD
*----------------------------------------------------------------------
&P       IM#SECT &SECT
&LABEL   IM#SECT LABEL            conditional label
&P.ID    DS    CL8                CSECT owning CPD
&P.L     DS    H                  32K max - CPD length
&P.ESTAE UESTAE MF=CPD,PFX=&P     U370STAE support fields
&P.BASE  DS    0D                 user's CPD fields follow
L$&P.ES  EQU   *-&P.ESTAE         length of CPD ESTAE fields
L$&P.PFX EQU   *-&P               length of CPD prefix
&P.USER  DS    0D                 user's CPD fields
         AIF   ('&GUPROCP(2)' EQ '').NOUSER
         AIF   ('&GUPROCP(2)' EQ '0').NOUSER
         DS    XL(&GUPROCP(2))    user's CPD additions
.NOUSER  ANOP
&P.USERX DS    0D                 end of user's CPD fields
L$&P.USR EQU   *-&P.USER          length of user CPD fields (align'd)
*
L$&P     EQU   *-&P               length of CPD
         POP   PRINT                                             DSACPD
         MEXIT
.*
.MFL     ANOP
&LABEL   DS    XL(L$CPDPFX)       CPD prefix                     DSACPD
         MEND
